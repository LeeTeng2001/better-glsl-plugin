{
  // Attributes to generate the parser definition
  parserClass="glsl.language.parser.GlslParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Glsl"
  psiImplClassSuffix="Impl"
  psiPackage="glsl.language.psi"  // location to generate
  psiImplPackage="glsl.language.psi.impl"

  elementTypeHolderClass="glsl.language.psi.GlslTypes"  // gets auto generated
  elementTypeClass="glsl.language.psi.GlslElementType"  // class to represent internal node
  tokenTypeClass="glsl.language.psi.GlslTokenType"  // represent terminal node

  // Utility
  psiImplUtilClass="glsl.language.psi.impl.GlslPsiImplUtil"
  extends(".*expr")=expr
}

glslFile ::= segment*
//glslFile ::= header segment*

//private header ::= '#version' 440
private segment ::= expression | declaration | COMMENT | CRLF

// Important top level segment
expression ::= expression_assign SEMICOLON
declaration ::= struct_definition | function_definition | variable_definition
//private rule_recover ::= !(SEMICOLON)

// Terminal token
storage_qualifier ::= const | in | out | attribute | uniform | varying | buffer | shared
identifier_type ::= void | int | uint | float | double | bool | var_name_type
assign_op ::= EQUAL | MUL_ASSIGN | DIV_ASSIGN | ADD_ASSIGN | SUB_ASSIGN | MOD_ASSIGN | LEFT_ASSIGN | RIGHT_ASSIGN | AND_ASSIGN | XOR_ASSIGN | OR_ASSIGN
arithmetic_op ::= PLUS | DASH | STAR | SLASH | PERCENT | LEFT_OP | RIGHT_OP | AMPERSAND | CARET | VERTICAL_BAR
relational_op ::= EQ_OP | ANGLE_L | ANGLE_R | GE_OP | LE_OP | NE_OP | AND_OP | OR_OP | XOR_OP

// Function definition
function_definition ::= identifier_type var_name_origin_func PAREN_L function_args? PAREN_R (C_BRACKET_L segment* C_BRACKET_R)? SEMICOLON? {pin=3}
function_args ::= identifier_type var_name_origin_variable (COMMA function_args)?

// Definition
struct_definition ::= struct var_name_origin_struct C_BRACKET_L variable_definition* C_BRACKET_R SEMICOLON {pin=1}
variable_definition ::= storage_qualifier? identifier_type var_name_origin_variable (EQUAL init_val)? SEMICOLON {pin=3}
init_val ::=  FLOAT_CONSTANT | INTEGER_CONSTANT | IDENTIFIER

// expression ------------------------------------------------------------
expression_assign ::= (expression_no_assign | expression_assign_paren) (assign_op expression_assign)?
expression_assign_paren ::= PAREN_L expression_assign PAREN_R
expression_no_assign ::= (expression_unit | expression_no_assign_paren) ((arithmetic_op | relational_op) expression_no_assign)?
expression_no_assign_paren ::= PAREN_L expression_no_assign PAREN_R
expression_unit ::= FLOAT_CONSTANT | INTEGER_CONSTANT | function_call | member_access  // function return member access??
function_call ::= var_name_access_func PAREN_L (expression_no_assign (COMMA expression_no_assign)*)? PAREN_R {pin=2}  // No assign or assign??
member_access ::= var_name_access_var (DOT var_name_access_member)* {pin=2}  // TODO: reference

// Swizzle built in type
//swizzle_access ::= IDENTIFIER DOT swizzle_prop? swizzle_prop? swizzle_prop? swizzle_prop?
//swizzle_prop ::= (swizzle_property_rgba | swizzle_property_stpq | swizzle_property_xyzw)
//private swizzle_property_xyzw ::= x | y | z | w
//private swizzle_property_rgba ::= r | g | b | a
//private swizzle_property_stpq ::= s | t | p | q

// variable origin for reference ------------------------
var_name_origin_func ::= IDENTIFIER {
  mixin="glsl.language.psi.impl.origin.GlslNamedFunctionDeclareImpl"
  implements="glsl.language.psi.GlslNamedElement"
  methods=[getName setName getNameIdentifier]
}

var_name_origin_struct ::= IDENTIFIER {
    mixin="glsl.language.psi.impl.origin.GlslNamedStructDeclareImpl"
    implements="glsl.language.psi.GlslNamedElement"
    methods=[getName setName getNameIdentifier]
}

var_name_origin_variable ::= IDENTIFIER {
    mixin="glsl.language.psi.impl.origin.GlslNamedVariableDeclareImpl"
    implements="glsl.language.psi.GlslNamedElement"
    methods=[getName setName getNameIdentifier]
}

// variable being used in other places
var_name_type ::= IDENTIFIER {
  mixin="glsl.language.psi.impl.access.GlslNamedVariableTypeImpl"
  implements="glsl.language.psi.GlslNamedElement"
  methods=[getName setName getNameIdentifier]
}

var_name_access_func ::= IDENTIFIER {
  mixin="glsl.language.psi.impl.access.GlslNamedVariableAccessFuncImpl"
  implements="glsl.language.psi.GlslNamedElement"
  methods=[getName setName getNameIdentifier]
}

var_name_access_var ::= IDENTIFIER {
  mixin="glsl.language.psi.impl.access.GlslNamedVariableAccessVarImpl"
  implements="glsl.language.psi.GlslNamedElement"
  methods=[getName setName getNameIdentifier]
}

var_name_access_member ::= IDENTIFIER {
  mixin="glsl.language.psi.impl.access.GlslNamedVariableAccessMemberImpl"
  implements="glsl.language.psi.GlslNamedElement"
  methods=[getName setName getNameIdentifier]
}

// Useless token to represent no token
private useless_expression ::= NULL_TOKEN
private recover_to_semicolon ::= !SEMICOLON
